/*
Copyright 2018 Square Inc.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
group 'com.squareup.readersdkflutterplugin'
version '2.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://sdk.squareup.com/android"
            credentials {
                username SQUARE_READER_SDK_APPLICATION_ID
                password SQUARE_READER_SDK_REPOSITORY_PASSWORD
            }
        }
    }
}

apply plugin: 'com.android.library'

def DEFAULT_PLAY_SERVICES_BASE_VERSION = '16.0.1'
def READER_SDK_VERSION = '[1.4.4, 2.0)'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    dexOptions {
        preDexLibraries true
        jumboMode true
        keepRuntimeAnnotatedClasses false
    }
}

dependencies {
    def readerSdkVersion = rootProject.hasProperty('readerSdkVersion') ? rootProject.readerSdkVersion : READER_SDK_VERSION
    def playServicesBaseVersion = rootProject.hasProperty('googlePlayServiceVersion') ? rootProject.googlePlayServiceVersion : DEFAULT_PLAY_SERVICES_BASE_VERSION
    implementation "com.google.android.gms:play-services-base:$playServicesBaseVersion"
    api "com.squareup.sdk.reader:reader-sdk-$SQUARE_READER_SDK_APPLICATION_ID:$readerSdkVersion"
    runtimeOnly "com.squareup.sdk.reader:reader-sdk-internals:$readerSdkVersion"
}
